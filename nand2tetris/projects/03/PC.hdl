// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    
    //3 way Or gate
    Or(a=inc, b=load, out=or1);
    Or(a=or1, b=reset, out=enable);

    //counter
    Register(in=regIn, load=enable, out=registerOut, out=out);

    //add 1 to register
    Inc16(in=registerOut, out=incOut);

    //load and reset selectors
    Mux16(a=incOut, b=in, sel=load, out=mux1);
    Mux16(a=mux1, b=false, sel=reset, out=regIn);
}
